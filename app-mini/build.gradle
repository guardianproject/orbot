apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 29
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "org.torproject.android.mini"
        minSdkVersion 16
        targetSdkVersion 29
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    aaptOptions {
        cruncherEnabled = false
    }

    flavorDimensions "free"

    productFlavors {
        mini {
            dimension "free"
            minSdkVersion 16
            applicationId 'org.torproject.android.mini'
            targetSdkVersion 28
            versionCode 10010000
            versionName 'mini-1.0.0-BETA-1-tor-0.4.0.4-rc'
            archivesBaseName = "Orbot-mini-$versionName"
        }
    }

    splits {

        // Configures multiple APKs based on ABI. This helps keep the size down, since PT binaries can be large.
        abi {

            // Enables building multiple APKs per ABI.
            enable true

            // By default all ABIs are included, so use reset() and include to specify that we only
            // want APKs for x86 and x86_64.

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            // Specifies a list of ABIs that Gradle should create APKs for.
            include "x86", "armeabi-v7a", "x86_64", "arm64-v8a"

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk false
        }

    }

    packagingOptions {
        exclude 'assets/arm/obfs4proxy'
        exclude 'lib/x86/obfs4proxy.so'
        exclude 'lib/x86_64/obfs4proxy.so'
        exclude 'lib/arm64-v8a/obfs4proxy.so'
        exclude 'lib/armeabi-v7a/obfs4proxy.so'
        exclude 'lib/armeabi/obfs4proxy.so'
    }

}


dependencies {
    implementation project(':orbotservice')
    implementation 'com.github.apl-devs:appintro:v4.2.2'
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation 'com.github.javiersantos:AppUpdater:2.7'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    androidTestImplementation "tools.fastlane:screengrab:1.2.0"
}

// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a': '1', 'arm64-v8a': '2', 'mips': '3', 'x86': '4', 'x86_64': '5']

import com.android.build.OutputFile

// For each APK output variant, override version code of outputs based on ABI codes
// ex) 'mips' -> 3xxx
// ex) 'x86' -> 4xxx
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (baseVersionCode != null) {
            output.versionCodeOverride = Integer.valueOf(baseVersionCode + variant.versionCode)
        }
    }
}
