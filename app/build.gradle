apply from: "../commons-device.gradle"
apply from : '../dependencies.gradle'

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    defaultConfig {
        applicationId "org.torproject.android"
        versionName getVersionName()
        minSdkVersion 16
        targetSdkVersion 31
    }
    lintOptions {
        htmlReport true
        xmlReport false
        textReport false
        lintConfig file("../lint.xml")
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
    }

    flavorDimensions "free"

    productFlavors {
        fullperm {
            dimension "free"

            minSdkVersion 16
            applicationId 'org.torproject.android'
            targetSdkVersion 30
            versionCode 1652200400

            versionName getVersionName()
            archivesBaseName = "Orbot-$versionName"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }

    dependencies {
        implementation(
                project(':appcore'),
                project(':intentintegrator'),
                project(':orbotservice'),
                libs.android_material,
                libs.android_snowfall,
                libs.android_volley,
                libs.androidx_localbroadcast,
                libs.apl_appintro,
                libs.bclogic_pulsator,
                libs.ipt_proxy

        )
        androidTestImplementation libs.fastlane_screengrab
    }
}
