apply plugin: 'com.android.application'


// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    signingConfigs {
        release {
            if (keystorePropertiesFile.canRead()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId "org.torproject.android"
        versionName getVersionName()
        minSdkVersion 16
        targetSdkVersion 30
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    aaptOptions {
        cruncherEnabled = false
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError true

        htmlReport true
        xmlReport false
        textReport false

        lintConfig file("../lint.xml")
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    aaptOptions {
        cruncherEnabled = false
    }

    flavorDimensions "free"

    productFlavors {
        fullperm {
            dimension "free"
            minSdkVersion 16
            applicationId 'org.torproject.android'
            targetSdkVersion 29
            versionCode 1633300100
            versionName getVersionName()
            archivesBaseName = "Orbot-$versionName"
        }
    }

    splits {

        // Configures multiple APKs based on ABI. This helps keep the size down, since PT binaries can be large.
        abi {

            // Enables building multiple APKs per ABI.
            enable true

            // By default all ABIs are included, so use reset() and include to specify that we only
            // want APKs for x86 and x86_64.

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            // Specifies a list of ABIs that Gradle should create APKs for.
            include "x86", "armeabi-v7a", "x86_64", "arm64-v8a"

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk true
        }
    }
}


dependencies {
    implementation project(':orbotservice')
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'pl.bclogic:pulsator4droid:1.0.3'
    implementation 'com.github.apl-devs:appintro:v4.2.2'
    implementation project(path: ':appcore')
    implementation project(path: ':intentintegrator')
    androidTestImplementation "tools.fastlane:screengrab:1.2.0"
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'info.pluggabletransports.aptds:apt-dispatch-library:1.0.9'
    implementation 'info.pluggabletransports.aptds:apt-meek-obfs4-legacy:1.0.9'
    implementation 'com.github.tladesignz:IPtProxy:0.5.2'
}

// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a': '1', 'arm64-v8a': '2', 'mips': '3', 'x86': '4', 'x86_64': '5']

import com.android.build.OutputFile

// For each APK output variant, override version code of outputs based on ABI codes
// ex) 'mips' -> 3xxx
// ex) 'x86' -> 4xxx
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (baseVersionCode != null) {
            output.versionCodeOverride = Integer.valueOf(variant.versionCode) + Integer.valueOf(baseVersionCode)
        }
    }
}
